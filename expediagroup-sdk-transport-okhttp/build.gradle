import kotlinx.kover.gradle.plugin.dsl.AggregationType
import kotlinx.kover.gradle.plugin.dsl.CoverageUnit

plugins {
    id 'maven-publish'
    id 'signing'
}

version = project.property("VERSION")
group = project.property("GROUP_ID")

apply from: "$rootDir/gradle-tasks/publish.gradle"
apply from: "$rootDir/gradle-tasks/signing.gradle"

dependencies {
    api 'com.squareup.okhttp3:okhttp:4.12.0'

    /* EG SDK Core */
    compileOnly 'com.expediagroup:expediagroup-sdk-core:1.0.0'

    /* Logging */
    compileOnly 'org.slf4j:slf4j-api:2.0.17'

    /* Testing */
    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'io.mockk:mockk:1.14.5'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    testImplementation(project(':expediagroup-sdk-core'))

    testImplementation 'org.slf4j:slf4j-api:2.0.17'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.named("check") {
    finalizedBy("koverHtmlReport")
}

kover {
    reports {
        total {
            verify {
                rule {
                    bound {
                        aggregationForGroup = AggregationType.COVERED_PERCENTAGE
                        coverageUnits = CoverageUnit.LINE
                        minValue = 100
                    }
                    bound {
                        aggregationForGroup = AggregationType.COVERED_PERCENTAGE
                        coverageUnits = CoverageUnit.BRANCH
                        minValue = 100
                    }
                }
            }
        }
    }
}
