import org.jetbrains.kotlin.gradle.plugin.KotlinBasePluginWrapper
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.1.10' apply false
    id "org.jlleitschuh.gradle.ktlint" version "12.1.2" apply false
    id 'org.jetbrains.kotlinx.kover' version "0.9.1" apply false
}

apply from: "$rootDir/gradle-tasks/publish-snapshot.gradle"

subprojects {
    /* Shared plugins between all modules */
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'org.jetbrains.kotlinx.kover'

    /* Shared repositories between all modules */
    repositories {
        mavenCentral()
        mavenLocal()
    }

    plugins.withId("org.jetbrains.kotlin.jvm") {
        kotlin {
            jvmToolchain(8)
        }
    }

    plugins.withType(KotlinBasePluginWrapper).configureEach {
        tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    ktlint {
        debug = true
        version = "1.5.0"
        verbose = true

        additionalEditorconfig = [
            "max_line_length"     : "200",
            "indent_style"        : "space",
            "indent_size"         : "4",
            "insert_final_newline": "true",
            "end_of_line"         : "lf",
        ]

        filter {
            exclude("**/testservice/**")
        }
    }
}
