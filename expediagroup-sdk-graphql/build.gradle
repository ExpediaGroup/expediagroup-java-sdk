import kotlinx.kover.gradle.plugin.dsl.AggregationType
import kotlinx.kover.gradle.plugin.dsl.CoverageUnit

plugins {
    id 'com.apollographql.apollo' version '4.3.2'
    id 'maven-publish'
    id 'signing'
}

version = project.property("VERSION")
group = project.property("GROUP_ID")

apply from: "$rootDir/gradle-tasks/publish.gradle"
apply from: "$rootDir/gradle-tasks/signing.gradle"

dependencies {
    /* Apollo */
    api 'com.apollographql.apollo:apollo-api:4.3.2'

    /* EG SDK Core */
    api 'com.expediagroup:expediagroup-sdk-core:1.0.0'

    /* Logging */
    compileOnly 'org.slf4j:slf4j-api:2.0.17'

    /* Testing */
    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'io.mockk:mockk:1.14.5'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'org.slf4j:slf4j-api:2.0.17'
}

apollo {
    service("testservice") {
        srcDir("src/test/graphql")
        packageName.set("testservice")
        generateDataBuilders.set(true)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.named("check") {
    finalizedBy("koverHtmlReport")
}

kover {
    reports {
        filters {
            excludes {
                packages(
                    "testservice",
                    "com.expediagroup.sdk.graphql.model",
                    "com.expediagroup.sdk.graphql.exception",
                    "com.expediagroup.sdk.graphql.paging.model",
                )
            }
        }
        total {
            verify {
                rule {
                    bound {
                        aggregationForGroup = AggregationType.COVERED_PERCENTAGE
                        coverageUnits = CoverageUnit.LINE
                        minValue = 100
                    }
                    bound {
                        aggregationForGroup = AggregationType.COVERED_PERCENTAGE
                        coverageUnits = CoverageUnit.BRANCH
                        minValue = 83
                    }
                }
            }
        }
    }
}
