import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.1'
}

version = project.property("VERSION")

apply from: "$rootDir/gradle-tasks/publish.gradle"

dependencies {
    implementation 'org.openapitools:openapi-generator:7.11.0'
    implementation 'org.openapitools:openapi-generator-gradle-plugin:7.11.0'
}

kotlin {
    jvmToolchain(11)
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '11'
    }
}


gradlePlugin {
    website = 'https://github.com/ExpediaGroup/expediagroup-java-sdk'
    vcsUrl = 'https://github.com/ExpediaGroup/expediagroup-java-sdk'
    plugins {
        create("egSdkOpenApiGenerator") {
            id = "com.expediagroup.sdk.openapigenerator"
            implementationClass = "com.expediagroup.sdk.openapigenerator.EgSdkOpenApiGeneratorPlugin"
            displayName = "EG SDK OpenApi Gradle Plugin Wrapper"
            description = "applies the standard OpenAPI Generator plugin (\"org.openapi.generator\") and configures it with Expedia Group SDK defaults."
            tags = ["expediagroup", "expedia", "sdk", "plugin", "openapi", "generator"]
        }
    }
}
